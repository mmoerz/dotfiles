#!/bin/zsh
# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.dotfiles"

# your project folder that we can `c [tab]` to
export PROJECTS="$HOME/src"

# your default editor
export EDITOR='vim'

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the functions
source $DOTFILES/zsh/fpath.zsh

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  [[ $DEBUG -gt 1 ]] && echo "sourcing $file"
  source "$file"
done

# load antidote
# Lazy-load antidote and generate the static load file only when needed
zsh_plugins=$HOME/.zsh_plugins
zsh_plugtxt=$(readlink ${zsh_plugins}.txt)
zsh_plugzsh=${zsh_plugins}.zsh
source $HOME/.antidote/antidote.zsh
if [[ ! -e ${zsh_plugzsh} || ${zsh_plugtxt} -nt ${zsh_plugzsh} ]]; then
  (
    antidote bundle <${zsh_plugins}.txt >${zsh_plugins}.zsh
  )
fi
source $zsh_plugins.zsh

# load everything but the path, completion and fpath files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh:#*/fpath.zsh}; do
  source "$file"
done

autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit
else
  compinit -C
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files updated_at

[ -f ~/.lscolors ] && . ~/.lscolors

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f ~/.localrc ] && . ~/.localrc

# avoid typing ssh key passwords all the time
if [ $SSH_AGENT_PID ]; then
    if [[ $(ssh-add -l) != *id_?sa* ]]; then
        ssh-add -t 2h  ## remember for 2 hours
    fi
fi
